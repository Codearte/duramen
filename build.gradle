plugins {
	id 'groovy'
	id 'java-gradle-plugin'
	id 'maven'
	id 'pl.allegro.tech.build.axion-release' version '1.5.0'
	id "com.bmuschko.nexus" version "2.3.1" apply false
	id "io.codearte.nexus-staging" version "0.5.3" apply false
	id "me.champeau.gradle.jmh" version "0.3.1"
}

scmVersion {
	tag {
		prefix = 'duramen'
	}
	hooks {
		pre 'fileUpdate', [file: 'README.md', pattern: { v, p -> /`io\.codearte\.duramen:duramen:.*`/ }, replacement: { v, p -> "`io.codearte.duramen:duramen:$v`" }]
	}
	createReleaseCommit = true
}

project.version = scmVersion.version
group = 'io.codearte.duramen'
description = 'Java persistent event bus'

sourceCompatibility = 1.7

repositories {
	mavenCentral()
}

ext {
	springVersion = "4.3.7.RELEASE"
	guavaVersion = "[11.0,)"
	slf4jVersion = "1.7.24"
	h2Version = "1.4.193"
	jacksonVersion = "2.8.7"
	jmhVersion = "1.18"
}

configurations{
	testCompile.exclude module: 'groovy-all'
}

dependencies {
	compile "org.slf4j:slf4j-api:${slf4jVersion}"
	compile "org.springframework:spring-context:${springVersion}"
	compile "org.springframework:spring-jdbc:${springVersion}"
	compile "com.google.guava:guava:${guavaVersion}"
	compile "com.h2database:h2:${h2Version}"
	compile "de.ruedigermoeller:fst:2.48"
	compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
	compile "net.openhft:collections:3.2.2"
	testCompile "org.springframework:spring-test:${springVersion}"
	testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
	testCompile "org.spockframework:spock-spring:1.0-groovy-2.4"
	testCompile 'org.hamcrest:hamcrest-core:1.3'
	testCompile "ch.qos.logback:logback-classic:1.1.1"
	testCompile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
	testCompile "com.jayway.awaitility:awaitility-groovy:1.7.0"
	testCompile 'org.kubek2k:springockito-annotations:1.0.9'
	jmh "org.openjdk.jmh:jmh-core:${jmhVersion}"
	jmh "org.openjdk.jmh:jmh-generator-annprocess:${jmhVersion}"
	jmh project.configurations.compile
}

test {
	testLogging {
		exceptionFormat = 'full'
	}
}

jmh {
	warmupIterations = 10
	iterations = 20
	fork = 1
	forceGC = true
}

wrapper {
	gradleVersion '3.4.1'
}
